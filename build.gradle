plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '6.25.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

group 'com.inwx'
version '4.0.0'

allprojects {
    repositories {
        mavenCentral()
    }

    project.pluginManager.withPlugin("com.diffplug.spotless") {
        spotless {
            java {
                licenseHeaderFile rootProject.file('extra/licensing/mit-license.java')
                removeUnusedImports()
                eclipse().configFile rootProject.file('extra/formatting/eclipse-java-google-style.xml')
                importOrderFile rootProject.file('extra/formatting/eclipse.importorder')
            }
            format("misc") {
                target("**/*.gradle", "**/*.md", "**/*.yml")
                trimTrailingWhitespace()
                endWithNewline()
            }
        }
    }

    tasks.withType(Jar) {
        from(project.rootDir) {
            include 'LICENSE'
            into 'META-INF'
        }
    }
}

dependencies {
    api 'com.google.code.gson:gson:2.11.0'
    implementation 'org.jboss.aerogear:aerogear-otp-java:1.0.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
}

apply from: './deploy.gradle'
