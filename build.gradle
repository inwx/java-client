plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'org.jreleaser' version '1.16.0'
    id 'application'
}

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

group 'com.inwx'
version '4.0.0'

allprojects {
    repositories {
        mavenCentral()
    }

    project.pluginManager.withPlugin("com.diffplug.spotless") {
        spotless {
            java {
                licenseHeaderFile rootProject.file('extra/licensing/mit-license.java')
                removeUnusedImports()
                eclipse().configFile rootProject.file('extra/formatting/eclipse-java-google-style.xml')
                importOrderFile rootProject.file('extra/formatting/eclipse.importorder')
            }
            format("misc") {
                target("**/*.gradle", "**/*.md", "**/*.yml")
                trimTrailingWhitespace()
                endWithNewline()
            }
        }
    }

    tasks.withType(Jar) {
        from(project.rootDir) {
            include 'LICENSE'
            into 'META-INF'
        }
    }
}

dependencies {
    api 'com.google.code.gson:gson:2.11.0'
    implementation 'org.jboss.aerogear:aerogear-otp-java:1.0.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
}

tasks.assembleDist.doFirst {
    mkdir layout.buildDirectory.dir("jreleaser")
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            from components.java

            pom {
                name = 'INWX Domrobot Java Client'
                description = 'INWX Domrobot Java Client'
                url = 'https://github.com/inwx/java-client'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'developer-inwx'
                        name = 'INWX Developer'
                        email = 'developer@inwx.de'
                        organization = 'INWX GmbH & Co. KG'
                        organizationUrl = 'https://www.inwx.com/en/'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/inwx/java-client.git'
                    developerConnection = 'scm:git:ssh@github.com:inwx/java-client.git'
                    url = 'https://github.com/inwx/java-client'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    environment {
        variables = 'config.yml'

        properties.put('JRELEASER_BRANCH', 'master')
    }

    project {
        authors = ['INWX Developer']
        license = 'MIT'

        links {
            homepage = 'https://github.com/inwx/java-client'
        }
        inceptionYear = '2014'
        description = 'INWX Domrobot Java Client'
    }

    release {
        github {
            repoOwner = 'inwx'
            overwrite = true
        }
    }
    distributions {
        app {
            distributionType = 'SINGLE_JAR'
            artifact {
                path = 'build/libs/domrobot-{{projectVersion}}.jar'
            }
        }
    }

    signing {
        active = 'ALWAYS'
        armored = true
    }

    deploy {
        maven {
            mavenCentral {
                app {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('target/staging-deploy')
                }
            }
        }
    }
}
