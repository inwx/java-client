plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.diffplug.spotless' version '5.15.2'
}

group 'com.inwx'
version '3.1.1'

allprojects {
    if (project.plugins.hasPlugin('java')) {
        sourceCompatibility = 1.7
    }

    repositories {
        mavenCentral()
    }

    project.pluginManager.withPlugin("com.diffplug.gradle.spotless") {
        spotless {
            java {
                licenseHeaderFile rootProject.file('extra/licensing/mit-license.java')
                removeUnusedImports()
                eclipse().configFile rootProject.file('extra/formatting/eclipse-java-google-style.xml')
                importOrderFile rootProject.file('extra/formatting/eclipse.importorder')
            }
            format("misc") {
                target("**/*.gradle", "**/*.md", "**/*.yml")
                trimTrailingWhitespace()
                endWithNewline()
            }
        }
    }

    tasks.withType(Jar) {
        from(project.rootDir) {
            include 'LICENSE'
            into 'META-INF'
        }
    }
}

dependencies {
    api 'com.google.code.gson:gson:2.8.8'
    implementation 'org.jboss.aerogear:aerogear-otp-java:1.0.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.13'
}

apply from: './deploy.gradle'
